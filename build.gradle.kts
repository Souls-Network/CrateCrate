import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    `java-library`
    id("org.spongepowered.gradle.plugin") version "2.2.0"
    id("com.github.johnrengelman.shadow") version "7.1.2"
}

group = "dev.flashlabs.cratecrate"
version = "0.0.1"

repositories {
    mavenCentral()
    maven("https://repo.spongepowered.org/maven/") {
        name = "spongepowered-repo"
    }
}

dependencies {
    implementation("com.h2database:h2:1.4.200")
}

sponge {
    apiVersion("12.0.0")
    license("MIT")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin("cratecrate") {
        entrypoint("dev.flashlabs.cratecrate.CrateCrate")
        displayName("CrateCrate")
        description("The cratest crate plugin of all time.")
        links {
            homepage("https://github.com/flash-labs/CrateCrate")
            source("https://github.com/flash-labs/CrateCrate")
            issues("https://github.com/flash-labs/CrateCrate/issues")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
        }
    }
}

val javaTarget = 21 // Sponge targets a minimum of Java 8
java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(javaTarget))
}

tasks.withType(JavaCompile::class).configureEach {
    options.apply {
        encoding = "utf-8" // Consistent source file encoding
        if (JavaVersion.current().isJava10Compatible) {
            release.set(javaTarget)
        }
    }
}

// Make sure all tasks which produce archives (jar, sources jar, javadoc jar, etc) produce more consistent output
tasks.withType(AbstractArchiveTask::class).configureEach {
    archiveVersion.set("v${project.version}")
    isReproducibleFileOrder = true
    isPreserveFileTimestamps = false
}

tasks.shadowJar {
    archiveClassifier.set("")
    dependencies {
        include(dependency("com.h2database:h2:1.4.200"))
    }
    relocate("org.h2", "${project.group}.shadow.org.h2")
}

tasks.build {
    dependsOn("shadowJar")
}
